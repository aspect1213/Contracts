#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ХранитьФайлыВТомахНаДиске = Константы.ХранитьФайлыВТомахНаДиске.Получить();
	ХранитьФайлыВТомахНаДискеПриОткрытии = ХранитьФайлыВТомахНаДиске;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТомаХраненияФайлов(Команда)
	ОткрытьФорму("Справочник.ТомаХраненияФайлов.ФормаСписка");
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройки(Команда)
	
	СохранитьНастройкиНаСервере();
	
	ОбновитьПовторноИспользуемыеЗначения();
	ОбновитьИнтерфейс();
	
	Если ХранениеВТомахНаДискеВключено Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПослеСохранитьНастройкиПродолжение",
			ЭтотОбъект);
		
		ПоказатьПредупреждение(
			ОписаниеОповещения,
			НСтр("ru = 'Включено хранение файлов в томах на диске, но тома еще не указаны.
				|Программа будет продолжать сохранять файлы в информационной базе до тех пор, пока не будет указан хотя бы один том хранения файлов.'"));
		Возврат;
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохранитьНастройкиПродолжение(ПараметрыЗаписи) Экспорт
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиНаСервере()
	
	Константы.ХранитьФайлыВТомахНаДиске.Установить(ХранитьФайлыВТомахНаДиске);
	
	ХранениеВТомахНаДискеВключено = ХранитьФайлыВТомахНаДиске И Не ХранитьФайлыВТомахНаДискеПриОткрытии;
	Если ХранениеВТомахНаДискеВключено Тогда
		
		Если ФайловыеФункции.ЕстьТомаХраненияФайлов() Тогда
			ХранениеВТомахНаДискеВключено = Ложь;
		КонецЕсли;
		
		// Создаем группу томов
		Группа = Справочники.ТомаХраненияФайлов.НайтиПоНаименованию(НСтр("ru = 'Группа по умолчанию'"), Истина);
		Если Не ЗначениеЗаполнено(Группа) Тогда
			Группа = Справочники.ТомаХраненияФайлов.СоздатьГруппу();
			Группа.Наименование = НСтр("ru = 'Группа по умолчанию'");
			Группа.Записать();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти