
// Сформировать заголовок группы Файлы
Функция КоличествоФайловВЗаголовок(КоличествоФайлов) Экспорт
	
	Заголовок = НСтр("ru = 'Файлы'");
	
	Если КоличествоФайлов <> 0 Тогда
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Файлы (%1)'"),
			КоличествоФайлов);
	КонецЕсли;
	
	Возврат Заголовок;
	
КонецФункции

// Возвращает строковое представление срока действия документа
// Параметры
//    Документ - СправочникСсылка.ДоговорныеДокументы - ссылка на документ
//
// Возвращаемое значение
//    Результат - строка - представление срока действия
Функция СформироватьПредставлениеСрокаДействия(Документ) Экспорт
	
	Результат = "";
	
	Если ЗначениеЗаполнено(Документ.ДатаНачалаДействия) Тогда
		Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'с %1'"),
			Формат(Документ.ДатаНачалаДействия, "ДЛФ=D")); 
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Документ.ДатаОкончанияДействия) Тогда
		ДобавитьЗначениеКСтрокеЧерезРазделитель(
			Результат,
			" ",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'по %1'"),
				Формат(Документ.ДатаОкончанияДействия, "ДЛФ=D")));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает Истина, если переданное значение является
// ссылкой на ДоговорныеДокументы или Объектом типа ДоговорныеДокументы
Функция ЭтоДоговорнойДокумент(Значение) Экспорт
	
	#Если Сервер Тогда
		Возврат ТипЗнч(Значение) = Тип("СправочникСсылка.ДоговорныеДокументы")
			Или ТипЗнч(Значение) = Тип("СправочникОбъект.ДоговорныеДокументы");
	#Иначе
		Возврат ТипЗнч(Значение) = Тип("СправочникСсылка.ДоговорныеДокументы");
	#КонецЕсли
	
КонецФункции

// Возвращает Истина, если переданное значение является
// ссылкой на Файлы или Объектом типа Файлы
Функция ЭтоФайл(Значение) Экспорт
	
	#Если Сервер Тогда
		Возврат ТипЗнч(Значение) = Тип("СправочникСсылка.Файлы")
			Или ТипЗнч(Значение) = Тип("СправочникОбъект.Файлы");
	#Иначе
		Возврат ТипЗнч(Значение) = Тип("СправочникСсылка.Файлы");
	#КонецЕсли
	
КонецФункции

Функция ЭтоДоговор(ТипДокумента) Экспорт 
	
	Возврат ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДоговорныхДокументов.Договор");
	
КонецФункции	

// Пересчитывает сумму в строке при изменении цены или количества
//
// Параметры:
//		ТекущаяСтрока - строка табличной части
// 		СтавкаНДС - ПеречислениеСсылка.СтавкиНДС - значение перечисления СтавкиНДС
//
Процедура ПересчитатьСуммуВСтрокеТЧ(ТекущаяСтрока, СтавкаНДС) Экспорт 
	
	Если ТекущаяСтрока <> Неопределено Тогда
		ТекущаяСтрока.Сумма = Окр(ТекущаяСтрока.Цена * ТекущаяСтрока.Количество, 2);
	КонецЕсли;
	
	ПересчитатьСуммуНДСВСтрокеТЧ(ТекущаяСтрока, СтавкаНДС);
	
КонецПроцедуры

// Пересчитывает цену в строке при изменении суммы
//
// Параметры:
//		ТекущаяСтрока - строка табличной части
// 		СтавкаНДС - ПеречислениеСсылка.СтавкиНДС - значение перечисления СтавкиНДС
//
Процедура ПересчитатьЦенуПоСуммеВСтрокеТЧ(ТекущаяСтрока, СтавкаНДС) Экспорт
	
	Если ТекущаяСтрока.Количество = 0 Тогда
		ТекущаяСтрока.Цена = ТекущаяСтрока.Сумма;
	Иначе
		ТекущаяСтрока.Цена = Окр(ТекущаяСтрока.Сумма / ТекущаяСтрока.Количество, 2);
	КонецЕсли;
	
	ПересчитатьСуммуНДСВСтрокеТЧ(ТекущаяСтрока, СтавкаНДС);
	
КонецПроцедуры

// Пересчитывает в строке сумму НДС от суммы в зависимости от включения НДС в цену
//
// Параметры:
//		ТекущаяСтрока - строка табличной части
// 		СтавкаНДС - ПеречислениеСсылка.СтавкиНДС - значение перечисления СтавкиНДС
//
Процедура ПересчитатьСуммуНДСВСтрокеТЧ(ТекущаяСтрока, СтавкаНДС) Экспорт
	
	НДСПоСтавкам4и2 = Ложь;
	
	ТекПроцентНДС = ПолучитьСтавкуНДСЧислом(СтавкаНДС, НДСПоСтавкам4и2);
	ТекущаяСтрока.СуммаНДС = РассчитатьСуммуНДС(ТекущаяСтрока.Сумма, ТекПроцентНДС);
	
КонецПроцедуры

// Возвращает числовое значение ставки НДС по значению перечисления
//
// Параметры:
// 		СтавкаНДС - ПеречислениеСсылка.СтавкиНДС - значение перечисления СтавкиНДС
//
// Возвращаемое значение:
// 		Число - Значение ставки НДС числом
//
Функция ПолучитьСтавкуНДСЧислом(Знач СтавкаНДС, НДСПоСтавкам4и2 = Ложь) 
	
	Если СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10") Тогда
		
		Возврат ?(НДСПоСтавкам4и2, 0.02, 0.1);
		
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18") Тогда 
		
		Возврат ?(НДСПоСтавкам4и2, 0.04, 0.18);
		
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20") Тогда 
		
		Возврат ?(НДСПоСтавкам4и2, 0.04, 0.20);
		
	Иначе
		
		Возврат 0;
		
	КонецЕсли;
	
КонецФункции

// Рассчитывает сумму НДС от суммы в зависимости от включения НДС в цену
//
// ЦенаВключаетНДС - Булево - Признак включения НДС в цену
// ПроцентНДС      - Число - Ставка НДС числом
// Сумма           - Число - Сумма, от которой необходимо рассчитать сумму НДС
//
Функция РассчитатьСуммуНДС(Сумма, ПроцентНДС, ЦенаВключаетНДС = Истина)
	
	Если ЦенаВключаетНДС Тогда
		СуммаНДС = Сумма * ПроцентНДС / (ПроцентНДС + 1);
	Иначе
		СуммаНДС = Сумма * ПроцентНДС;
	КонецЕсли;
	
	Возврат СуммаНДС;
	
КонецФункции
