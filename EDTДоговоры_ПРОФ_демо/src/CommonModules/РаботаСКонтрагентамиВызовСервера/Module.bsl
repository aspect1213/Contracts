#Область ПрограммныйИнтерфейс

// Находит дубликаты контрагента и помещает их во временное хранилище.
//
Функция НайтиДублиКонтрагента(Ссылка, ИНН, КПП) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Ссылка,
		|	Контрагенты.Наименование КАК Наименование,
		|	ВЫРАЗИТЬ(Контрагенты.НаименованиеПолное КАК СТРОКА(100)) КАК НаименованиеПолное,
		|	Контрагенты.ИНН КАК ИНН,
		|	Контрагенты.КПП КАК КПП,
		|	КОЛИЧЕСТВО(Документы.Ссылка) КАК КоличествоДокументов
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.ДоговорныеДокументы КАК Документы
		|ПО
		|	Документы.Контрагент = Контрагенты.Ссылка
		|	И НЕ Документы.ПометкаУдаления
		|ГДЕ
		|	Контрагенты.Ссылка <> &Ссылка
		|	И НЕ Контрагенты.ПометкаУдаления
		|	И (Контрагенты.ИНН = &ИНН И Контрагенты.КПП = &КПП)
		|СГРУППИРОВАТЬ ПО
		|	Контрагенты.Ссылка,
		|	Контрагенты.Наименование,
		|	ВЫРАЗИТЬ(Контрагенты.НаименованиеПолное КАК СТРОКА(100)),
		|	Контрагенты.ИНН,
		|	Контрагенты.КПП
		|АВТОУПОРЯДОЧИВАНИЕ");
		
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ИНН", СокрЛП(ИНН));
	Запрос.УстановитьПараметр("КПП", СокрЛП(КПП));
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Дубликаты = Результат.Выгрузить();
	Возврат ПоместитьВоВременноеХранилище(Дубликаты, Новый УникальныйИдентификатор);
	
КонецФункции

// Возвращает стандартную расшифрорвку подписи по ФИО.
//
Функция ПолучитьРасшифровкуПодписи(Знач ФИО) Экспорт
	
	ФИО = СокрЛП(ФИО);
	МассивСлов = СтрРазделить(ФИО, " .", Ложь);
	
	Если МассивСлов.Количество() = 3 Тогда
		Возврат СтрШаблон("%1.%2. %3",
			Лев(МассивСлов[1], 1),
			Лев(МассивСлов[2], 1),
			МассивСлов[0]);
	Иначе
		Возврат ФИО;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти