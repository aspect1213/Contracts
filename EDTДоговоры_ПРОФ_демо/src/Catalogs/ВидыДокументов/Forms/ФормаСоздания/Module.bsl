
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Родитель") И ЗначениеЗаполнено(Параметры.Родитель) Тогда 
		ТипДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Родитель, "ВидДокумента.Тип");
		
		Если ТипДокумента = Перечисления.ТипыДоговорныхДокументов.Договор Тогда 
			
			Типы.Добавить(Перечисления.ТипыДоговорныхДокументов.ДопСоглашение, "Допсоглашение");
			Типы.Добавить(Перечисления.ТипыДоговорныхДокументов.Акт);
			Типы.Добавить(Перечисления.ТипыДоговорныхДокументов.Накладная);
			Типы.Добавить(Перечисления.ТипыДоговорныхДокументов.Счет);
			
		ИначеЕсли ТипДокумента = Перечисления.ТипыДоговорныхДокументов.ДопСоглашение Тогда 
			
			Типы.Добавить(Перечисления.ТипыДоговорныхДокументов.Акт);
			Типы.Добавить(Перечисления.ТипыДоговорныхДокументов.Накладная);
			Типы.Добавить(Перечисления.ТипыДоговорныхДокументов.Счет);
			
		ИначеЕсли ТипДокумента = Перечисления.ТипыДоговорныхДокументов.Акт 
			Или ТипДокумента = Перечисления.ТипыДоговорныхДокументов.Накладная Тогда
			
			Типы.Добавить(Перечисления.ТипыДоговорныхДокументов.Счет);
			
		КонецЕсли;
	Иначе 
		Типы.Добавить(Перечисления.ТипыДоговорныхДокументов.Договор);
		Типы.Добавить(Перечисления.ТипыДоговорныхДокументов.ДопСоглашение, "Допсоглашение");
		Типы.Добавить(Перечисления.ТипыДоговорныхДокументов.Акт);
		Типы.Добавить(Перечисления.ТипыДоговорныхДокументов.Накладная);
		Типы.Добавить(Перечисления.ТипыДоговорныхДокументов.Счет);
	КонецЕсли;
	
	КтоГотовит.Добавить(Перечисления.КтоГотовит.Организация, НСтр("ru = 'Документ готовим мы'"));
	КтоГотовит.Добавить(Перечисления.КтоГотовит.Контрагент, НСтр("ru = 'Приходит от контрагента'"));
	
	Если Параметры.Свойство("РежимСозданияДоговора") Тогда 
		РежимСозданияДоговора = Параметры.РежимСозданияДоговора;
	КонецЕсли;
	
	Если Параметры.Свойство("НеСоздаватьДокументПриЗакрытии") Тогда 
		НеСоздаватьДокументПриЗакрытии = Параметры.НеСоздаватьДокументПриЗакрытии;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбранныйТип = Типы.НайтиПоИдентификатору(ВыбраннаяСтрока).Значение;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаКтоГотовит;
	
КонецПроцедуры

&НаКлиенте
Процедура КтоГотовитВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбранныйКтоГотовит = КтоГотовит.НайтиПоИдентификатору(ВыбраннаяСтрока).Значение;
	
	ОткрытьФормуНовогоВида();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Далее(Команда)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаТипы Тогда 
	
		Если Элементы.Типы.ТекущиеДанные = Неопределено Тогда 
			ВыбранныйТип = Типы[0].Значение;
		Иначе
			ВыбранныйТип = Элементы.Типы.ТекущиеДанные.Значение;
		КонецЕсли;	
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаКтоГотовит;
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаКтоГотовит Тогда 
		
		Если Элементы.КтоГотовит.ТекущиеДанные = Неопределено Тогда 
			ВыбранныйКтоГотовит = КтоГотовит[0].Значение;
		Иначе
			ВыбранныйКтоГотовит = Элементы.КтоГотовит.ТекущиеДанные.Значение;
		КонецЕсли;	
			
		ОткрытьФормуНовогоВида();
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФормуНовогоВида()
	
	Модифицированность = Ложь;
	Закрыть();
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Тип", ВыбранныйТип);
	ЗначенияЗаполнения.Вставить("КтоГотовит", ВыбранныйКтоГотовит);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	Если РежимСозданияДоговора Тогда 
		ПараметрыФормы.Вставить("РежимСозданияДоговора", РежимСозданияДоговора);
	КонецЕсли;	
	
	Если НеСоздаватьДокументПриЗакрытии Тогда 
		ПараметрыФормы.Вставить("НеСоздаватьДокументПриЗакрытии", НеСоздаватьДокументПриЗакрытии);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.ВидыДокументов.Форма.ФормаЭлемента", ПараметрыФормы, ВладелецФормы);
	
КонецПроцедуры	

#КонецОбласти
