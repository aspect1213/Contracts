
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Тип = Параметры.Тип;
	ФорматНомера = Параметры.ФорматНомера;
	ВидДокумента = Параметры.ВидДокумента;
	СледующийНомер = Параметры.СледующийНомер;
	
	Если Не ЗначениеЗаполнено(ФорматНомера) Тогда 
		ФорматНомера = НСтр("ru = '[Номер]'");
	КонецЕсли;
	
	ЭтоДоговор = (Тип = Перечисления.ТипыДоговорныхДокументов.Договор);
	Если ЭтоДоговор Тогда 
		Элементы.НомерДокументаОснования.Видимость = Ложь;
	КонецЕсли;	
	
	Если ЭтоДоговор Тогда 
		Элементы.СледующийНомер.Видимость = Истина;
		Если СледующийНомер > 1 Тогда 
			Элементы.СледующийНомер.Заголовок = НСтр("ru = 'Следующий'");
			Элементы.СледующийНомер.Подсказка = НСтр("ru = 'Следующий номер'");
		КонецЕсли;
	Иначе	
		Элементы.СледующийНомер.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СформироватьПримерНомера();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФорматНомераПриИзменении(Элемент)
	
	СформироватьПримерНомера();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДеньМесяца(Команда)
	
	ФорматНомера = ФорматНомера + "[День]";
	СформироватьПримерНомера();
	
КонецПроцедуры

&НаКлиенте
Процедура НомерМесяца(Команда)
	
	ФорматНомера = ФорматНомера + "[Месяц]";
	СформироватьПримерНомера();
	
КонецПроцедуры

&НаКлиенте
Процедура НомерКвартала(Команда)
	
	ФорматНомера = ФорматНомера + "[Квартал]";
	СформироватьПримерНомера();
	
КонецПроцедуры

&НаКлиенте
Процедура Год2(Команда)
	
	ФорматНомера = ФорматНомера + "[Год2]";
	СформироватьПримерНомера();
	
КонецПроцедуры

&НаКлиенте
Процедура Год4(Команда)
	
	ФорматНомера = ФорматНомера + "[Год4]";
	СформироватьПримерНомера();
	
КонецПроцедуры

&НаКлиенте
Процедура ИндексОтветственного(Команда)
	
	ФорматНомера = ФорматНомера + "[ИндексОтв]";
	СформироватьПримерНомера();
	
КонецПроцедуры

&НаКлиенте
Процедура НомерДокументаОснования(Команда)
	
	ФорматНомера = ФорматНомера + "[НомерДокОсн]";
	СформироватьПримерНомера();
	
КонецПроцедуры

&НаКлиенте
Процедура Номер(Команда)
	
	ФорматНомера = ФорматНомера + "[Номер]";
	СформироватьПримерНомера();
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("ФорматНомера", СокрЛП(ФорматНомера));
	ПараметрыЗакрытия.Вставить("ПримерНомера", СокрЛП(ПримерНомера));
	ПараметрыЗакрытия.Вставить("СледующийНомер", СледующийНомер);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СформироватьПримерНомера()
	
	ОписаниеОшибки = "";
	Если Не Нумерация.СформироватьПримерНомера(ФорматНомера, ПримерНомера, ОписаниеОшибки) Тогда 
		ПримерНомера = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка в формате номера: %1'"), ОписаниеОшибки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
