#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоказыватьУдаленные = Ложь;
	ПоказатьУдаленные();
	
	ДоговорныеДокументы.СписокДокументовУсловноеОформлениеПомеченныхНаУдаление(Список);
	
	Если Параметры.Свойство("НеСоздаватьДокументПриЗакрытии") Тогда 
		НеСоздаватьДокументПриЗакрытии = Параметры.НеСоздаватьДокументПриЗакрытии;
	КонецЕсли;
	
	Если Параметры.Свойство("ЗагруженИзПочты") Тогда 
		ЗагруженИзПочты = Параметры.ЗагруженИзПочты;
	КонецЕсли;
	
	Если Параметры.Свойство("ОткрытаИзПомощникаНачалаРаботы") Тогда
		ОткрытаИзПомощникаНачалаРаботы = Истина;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ПоказыватьУдаленные = Настройки["ПоказыватьУдаленные"];
	ПоказатьУдаленные();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	СоздатьНовыйВидДокумент(Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если Элементы.Список.ТекущаяСтрока = Неопределено Тогда 
		Элементы.ФормаСоздатьДокумент.Доступность = Ложь;
	Иначе	
		Элементы.ФормаСоздатьДокумент.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	
	ПоказатьУдаленные();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокумент(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ВидДокумента", ТекущаяСтрока);
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	КлючеваяОперация = "ДокументыВыполнениеКомандыСоздать";
	ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени(КлючеваяОперация);
	
	Открытьформу("Справочник.ДоговорныеДокументы.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВидДокумента(Команда)
	
	СоздатьНовыйВидДокумент(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СоздатьНовыйВидДокумент(Копирование)
	
	ПараметрыФормы = Новый Структура;
	Если НеСоздаватьДокументПриЗакрытии Тогда 
		ПараметрыФормы.Вставить("НеСоздаватьДокументПриЗакрытии", НеСоздаватьДокументПриЗакрытии);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ЗагруженИзПочты", ЗагруженИзПочты);
	
	Если Копирование Тогда
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда 
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ЗначениеКопирования", ТекущиеДанные.Ссылка);
		КонецЕсли;
	КонецЕсли; 
	
	ОткрытьФорму("Справочник.ВидыДокументов.ФормаОбъекта", ПараметрыФормы, Элементы.Список);
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьУдаленные()
	
	Если ПоказыватьУдаленные Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "ПометкаУдаления");
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПометкаУдаления", Ложь);
	КонецЕсли;
	
	Элементы.ПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ОткрытаИзПомощникаНачалаРаботы Тогда
		ПодключитьОбработчикОжидания("ЗапускИзПомощникаНачалаРаботы", 0.1, Истина);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапускИзПомощникаНачалаРаботы()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Текст", 
		НСтр("ru = '<BODY>
              |<p>А сейчас создайте виды документов, с которыми вы будете работать в программе.</p>
              |<p>Начните с одного-двух самых важных видов договоров.</p>
              |</BODY>'")
		);
	ПараметрыФормы.Вставить("ЗаголовокКнопкиЗакрыть", "Приступить");	
	
	ОткрытьФорму("ОбщаяФорма.Подсказка", ПараметрыФормы, ЭтаФорма);

КонецПроцедуры

#КонецОбласти
