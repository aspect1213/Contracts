#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ПредыдущиеЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка,
			"ПометкаУдаления");
	Иначе
		ПредыдущиеЗначенияРеквизитов = Новый Структура;
		ПредыдущиеЗначенияРеквизитов.Вставить("ПометкаУдаления", Ложь);
	КонецЕсли;
	
	// Подсистема Свойства
	УправлениеСвойствами.ПередЗаписьюВидаОбъекта(ЭтотОбъект, "Справочник_ДоговорныеДокументы");
	
	Если Не Ссылка.Пустая() Тогда 
		ПредыдущееНаименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Наименование");
		
		Если Наименование <> ПредыдущееНаименование Тогда 
			ДополнительныеСвойства.Вставить("ПереименоватьСвязанныеДокументы", Истина);
		КонецЕсли;
	КонецЕсли;
	
	Если Тип = Перечисления.ТипыДоговорныхДокументов.Договор Тогда 
		НаименованиеПолное = Наименование + " " + НСтр("ru = '(договор)'");
	ИначеЕсли Тип = Перечисления.ТипыДоговорныхДокументов.Акт Тогда 
		НаименованиеПолное = Наименование + " " + НСтр("ru = '(акт)'");
	ИначеЕсли Тип = Перечисления.ТипыДоговорныхДокументов.ДопСоглашение Тогда 
		НаименованиеПолное = Наименование + " " + НСтр("ru = '(допсоглашение)'");
	ИначеЕсли Тип = Перечисления.ТипыДоговорныхДокументов.Накладная Тогда 
		НаименованиеПолное = Наименование + " " + НСтр("ru = '(накладная)'");
	ИначеЕсли Тип = Перечисления.ТипыДоговорныхДокументов.Счет Тогда 
		НаименованиеПолное = Наименование + " " + НСтр("ru = '(счет)'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ФорматНомера) Тогда 
		СпособНумерации = Перечисления.СпособыНумерации.Автоматически;
	Иначе
		СпособНумерации = Перечисления.СпособыНумерации.Вручную;
	КонецЕсли;	
	
	// Пометка на удаление приложенных файлов.
	Если ПометкаУдаления <> ПредыдущиеЗначенияРеквизитов.ПометкаУдаления Тогда 
		
		РаботаСФайламиВызовСервера.ПометитьНаУдалениеПриложенныеФайлы(Ссылка, ПометкаУдаления);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ДополнительныеСвойства.Свойство("ПереименоватьСвязанныеДокументы") 
		И ДополнительныеСвойства.ПереименоватьСвязанныеДокументы Тогда 
		ПереименоватьСвязанныеДокументы(Ссылка);
	КонецЕсли;

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	НаборСвойств = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПереименоватьСвязанныеДокументы(ВидДокумента)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоговорныеДокументы.Ссылка
		|ИЗ
		|	Справочник.ДоговорныеДокументы КАК ДоговорныеДокументы
		|ГДЕ
		|	ДоговорныеДокументы.ВидДокумента = &ВидДокумента";
	Запрос.Параметры.Вставить("ВидДокумента", ВидДокумента);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ДокументОбъект.Записать();
			РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
