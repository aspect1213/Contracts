
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Документ = Параметры.Документ;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, 
		"Документ", Документ);
		
	Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Контрагент");
	
	Заголовок = СтрШаблон(НСтр("ru = 'Журнал передачи документа %1'"),
		Строка(Документ));
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИСТИНА
		|ИЗ
		|	РегистрСведений.ЖурналПередачи
		|ГДЕ
		|	Документ = &Документ И НЕ Возвращен";
	Запрос.УстановитьПараметр("Документ", Документ);
	
	Элементы.ОтметитьВозврат.Видимость = Не Запрос.Выполнить().Пустой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Элементы.ОтметитьВозврат.Доступность = Ложь;
		Возврат;
	КонецЕсли;	
	
	Элементы.ОтметитьВозврат.Доступность = Не ТекущиеДанные.Возвращен;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Режим", "Передать");
	ПараметрыФормы.Вставить("Документ", Документ);
	ПараметрыФормы.Вставить("Пользователь", Контрагент);
	
	ОткрытьФорму("РегистрСведений.ЖурналПередачи.ФормаЗаписи", ПараметрыФормы,,,,,, 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтметитьВозврат(Команда)
	
	УстановитьВозвращен(Документ);
	
	ПоказатьОповещениеПользователя(
			НСтр("ru = 'Документ отмечен как возвращенный от контрагента'"), 
			ПолучитьНавигационнуюСсылку(Документ));
			
	Оповестить("ИзмененЖурналПередачи", Документ);
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура УстановитьВозвращен(Документ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЖурналПередачи.Период КАК Период,
		|	ЖурналПередачи.Документ КАК Документ
		|ИЗ
		|	РегистрСведений.ЖурналПередачи КАК ЖурналПередачи
		|ГДЕ
		|	ЖурналПередачи.Документ = &Документ
		|	И ЖурналПередачи.Возвращен = ЛОЖЬ";
	Запрос.УстановитьПараметр("Документ", Документ);
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		МенеджерЗаписи = РегистрыСведений.ЖурналПередачи.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Документ = Выборка.Документ;
		МенеджерЗаписи.Период = Выборка.Период;
		МенеджерЗаписи.Прочитать();
		
		Если МенеджерЗаписи.Возвращен Тогда 
			Возврат;
		КонецЕсли;	
	
		МенеджерЗаписи.Возвращен = Истина;
		МенеджерЗаписи.ДатаВозврата = ТекущаяДата();
		МенеджерЗаписи.Записать();
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти
