
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Показывать = "Невозвращенные";
	УстановитьОтбор();
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(Элементы.Показывать, Показывать, "Все");
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УстановитьОтбор();
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(Элементы.Показывать, Показывать, "Все");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Элементы.ОтметитьВозврат.Доступность = Ложь;
		Возврат;
	КонецЕсли;	
	
	Элементы.ОтметитьВозврат.Доступность = Не ТекущиеДанные.Возвращен;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьПриИзменении(Элемент)
	
	УстановитьОтбор();
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(Элемент, Показывать, "Все");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Показывать = "Все";
	УстановитьОтбор();
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(Элементы.Показывать, Показывать, "Все");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтметитьВозврат(Команда)
	
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ВыделенныеСтроки.Количество() = 1 Тогда 
		Если ТипЗнч(ВыделенныеСтроки[0]) <> Тип("РегистрСведенийКлючЗаписи.ЖурналПередачи") Тогда 
			ПоказатьПредупреждение(, НСтр("ru = 'Команда не может быть выполнена для указанного объекта.'"));
			Возврат;
		КонецЕсли;
		Если Элементы.Список.ТекущиеДанные.Возвращен Тогда 
			ПоказатьПредупреждение(, НСтр("ru = 'Документ уже возвращен!'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;	
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		Если ТипЗнч(ВыделеннаяСтрока) = Тип("РегистрСведенийКлючЗаписи.ЖурналПередачи") Тогда 
			УстановитьВозвращен(ВыделеннаяСтрока);
		КонецЕсли;
	КонецЦикла;	
	
	Если ВыделенныеСтроки.Количество() = 1 Тогда 
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда 
			Оповестить("ИзмененЖурналПередачи", ТекущиеДанные.Документ);
		КонецЕсли;	
	КонецЕсли;	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура УстановитьВозвращен(КлючЗаписи)
	
	МенеджерЗаписи = РегистрыСведений.ЖурналПередачи.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период = КлючЗаписи.Период;
	МенеджерЗаписи.Документ = КлючЗаписи.Документ;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Возвращен Тогда 
		Возврат;
	КонецЕсли;	
	
	МенеджерЗаписи.Возвращен = Истина;
	МенеджерЗаписи.ДатаВозврата = ТекущаяДата();
	МенеджерЗаписи.Записать();
	
КонецПроцедуры	

&НаСервере
Процедура УстановитьОтбор()
	
	ПараметрыОтбора = Новый Соответствие();
	ПараметрыОтбора.Вставить("Показывать", Показывать);
	УстановитьОтборСписка(ПараметрыОтбора);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборСписка(ПараметрыОтбора)

	Если ЗначениеЗаполнено(ПараметрыОтбора["ПоПользователю"]) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Пользователь", ПараметрыОтбора["ПоПользователю"]);
		Элементы.Пользователь.Видимость = Ложь;
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Список.Отбор, "Пользователь");
		Элементы.Пользователь.Видимость = Истина;
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ПараметрыОтбора["ПоДокументу"]) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Документ", ПараметрыОтбора["ПоДокументу"]);
		Элементы.Документ.Видимость = Ложь;
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Список.Отбор, "Документ");
		Элементы.Документ.Видимость = Истина;
	КонецЕсли;	
	
	Если ПараметрыОтбора["Показывать"] = "Все" Или Не ЗначениеЗаполнено(ПараметрыОтбора["Показывать"]) Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Список.Отбор, "Возвращен"); 
		Элементы.СрокВозврата.Видимость = Истина;
		Элементы.ДатаВозврата.Видимость = Истина;
	ИначеЕсли ПараметрыОтбора["Показывать"] = "Возвращенные" Тогда 
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Возвращен", Истина);
		Элементы.СрокВозврата.Видимость = Ложь;
		Элементы.ДатаВозврата.Видимость = Истина;
	ИначеЕсли ПараметрыОтбора["Показывать"] = "Невозвращенные" Тогда 
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Возвращен", Ложь);
		Элементы.СрокВозврата.Видимость = Истина;
		Элементы.ДатаВозврата.Видимость = Ложь;
	КонецЕсли;
		
КонецПроцедуры	

#КонецОбласти
