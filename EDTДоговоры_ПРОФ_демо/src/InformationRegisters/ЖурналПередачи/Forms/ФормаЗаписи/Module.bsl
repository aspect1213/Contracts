
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоНоваяЗапись = Параметры.Ключ.Пустой();
	
	Если Параметры.Свойство("Режим") И ЗначениеЗаполнено(Параметры.Режим) Тогда 
		
		Если Параметры.Режим = "СоздатьПоКонтрагенту" Тогда
			КлючНазначенияИспользования = "СДокументом";
			Запись.Пользователь = Параметры.Пользователь;
			Элементы.Возвращен.Видимость = Ложь;
			
			НовыйПараметрВыбора = Новый ПараметрВыбора(
				"Отбор.Контрагент", Параметры.Пользователь);
			
			ПараметрыВыбора = Новый Массив;
			ПараметрыВыбора.Добавить(НовыйПараметрВыбора);
			Элементы.Документ.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
			
		Иначе 
			Запись.Документ = Параметры.Документ;
			Элементы.Документ.Видимость = Ложь;
			КлючНазначенияИспользования = "БезДокумента";
			
			Запись.Пользователь = Параметры.Пользователь;
			
			Если Параметры.Режим = "Передать" Тогда 
				
				Элементы.Возвращен.Видимость = Ложь;
				
			ИначеЕсли Параметры.Режим = "Открыть" Тогда 
				
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	ЖурналПередачи.Период КАК Период,
				|	ЖурналПередачи.Документ КАК Документ
				|ИЗ
				|	РегистрСведений.ЖурналПередачи КАК ЖурналПередачи
				|ГДЕ
				|	ЖурналПередачи.Документ = &Документ
				|	И ЖурналПередачи.Пользователь = &Пользователь
				|	И ЖурналПередачи.Возвращен = ЛОЖЬ";
				Запрос.УстановитьПараметр("Документ", Параметры.Документ);
				Запрос.УстановитьПараметр("Пользователь", Параметры.Пользователь);
				
				Выборка = Запрос.Выполнить().Выбрать();
				Если Выборка.Следующий() Тогда 
					МенеджерЗаписи = РегистрыСведений.ЖурналПередачи.СоздатьМенеджерЗаписи();
					МенеджерЗаписи.Документ = Выборка.Документ;
					МенеджерЗаписи.Период = Выборка.Период;
					МенеджерЗаписи.Прочитать();
					
					ЗначениеВРеквизитФормы(МенеджерЗаписи, "Запись");
					ЭтоНоваяЗапись = Ложь;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда 
			Запись.Период = ТекущаяДатаСеанса();
			Запись.СрокВозврата = '00010101';
			Запись.Возвращен = Ложь;
			Запись.ДатаВозврата = '00010101';
		КонецЕсли;
		
		ЗначенияЗаполнения = Параметры.ЗначенияЗаполнения;
		Если ТипЗнч(ЗначенияЗаполнения) = Тип("Структура")
			И ЗначенияЗаполнения.Свойство("Документ")
			И ЗначениеЗаполнено(ЗначенияЗаполнения.Документ)
			И ЗначениеЗаполнено(Запись.Документ) Тогда 
			
			Запись.Пользователь = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.Документ, "Контрагент");
			Элементы.Документ.Видимость = Ложь;
			КлючНазначенияИспользования = "БезДокумента";
		Иначе
			Элементы.Документ.Видимость = Истина;
			КлючНазначенияИспользования = "СДокументом";
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЭтоНоваяЗапись Тогда 
		Элементы.Возвращен.Видимость = Ложь;
	Иначе	
		Элементы.Возвращен.Видимость = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.Пользователь) Тогда 
		АвтоЗаголовок = Ложь;
		Если ЭтоНоваяЗапись Тогда 
			Заголовок = СтрШаблон(НСтр("ru = 'Запись передачи документа контрагенту %1'"), 
				Строка(Запись.Пользователь));
		Иначе
			Заголовок = СтрШаблон(НСтр("ru = 'Документ находится у контрагента %1'"), 
				Строка(Запись.Пользователь));
		КонецЕсли;
	Иначе		
		АвтоЗаголовок = Истина;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаписатьИЗакрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Запись.Возвращен Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЭтоНоваяЗапись И Не ПараметрыЗаписи.Свойство("ПродолжитьЗаписьПослеВопроса") Тогда 
		ТекстВопроса = "";
		СтруктураЗаписи = Новый Структура;
		СтруктураЗаписи.Вставить("Документ", Запись.Документ);
		СтруктураЗаписи.Вставить("Пользователь", Запись.Пользователь);
		СтруктураЗаписи.Вставить("Период", Запись.Период);
		
		ОтразитьВозвратОтПрежнегоДержателя(СтруктураЗаписи);
		ПараметрыЗаписи.Вставить("ПродолжитьЗаписьПослеВопроса", Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзмененЖурналПередачи", Запись.Документ);
	ОповеститьОбИзменении(Тип("СправочникСсылка.ДоговорныеДокументы"));
	ЭтоНоваяЗапись = Ложь;
	
	Если ПараметрыЗаписи.Свойство("Закрыть") Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Запись.СрокВозврата) И КонецДня(Запись.СрокВозврата) < Запись.Период Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрШаблон(НСтр("ru = 'Срок возврата меньше даты передачи (%1)'"),
				Формат(Запись.Период, "ДЛФ=D")),,
			"Запись.СрокВозврата",,
			Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДокументПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Запись.Документ) Тогда 
		Запись.Пользователь = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(Запись.Документ, "Контрагент");
	Иначе	
		Запись.Пользователь = Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.Пользователь) Тогда 
		АвтоЗаголовок = Ложь;
		Если ЭтоНоваяЗапись Тогда 
			Заголовок = СтрШаблон(НСтр("ru = 'Запись передачи документа контрагенту %1'"), 
				Строка(Запись.Пользователь));
		Иначе
			Заголовок = СтрШаблон(НСтр("ru = 'Документ находится у контрагента %1'"), 
				Строка(Запись.Пользователь));
		КонецЕсли;
	Иначе		
		АвтоЗаголовок = Истина;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	ЗаписатьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаписатьИЗакрыть()
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("Закрыть", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтразитьВозвратОтПрежнегоДержателя(СтруктураЗаписи)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЖурналПередачиДокументов.Период,
	|	ЖурналПередачиДокументов.Документ,
	|	ЖурналПередачиДокументов.Возвращен
	|ИЗ
	|	РегистрСведений.ЖурналПередачи КАК ЖурналПередачиДокументов
	|ГДЕ
	|	ЖурналПередачиДокументов.Документ = &Документ
	|	И НЕ ЖурналПередачиДокументов.Возвращен";
	
	Запрос.УстановитьПараметр("Документ", СтруктураЗаписи.Документ);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если Не Выборка.Возвращен Тогда
			МенеджерЗаписи = РегистрыСведений.ЖурналПередачи.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Период = Выборка.Период;
			МенеджерЗаписи.Документ = СтруктураЗаписи.Документ;
			МенеджерЗаписи.Прочитать();
			
			МенеджерЗаписи.Возвращен = Истина;
			МенеджерЗаписи.Записать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвращенПриИзменении(Элемент)
	
	Если Запись.Возвращен Тогда
		Запись.ДатаВозврата = ТекущаяДата();
	Иначе
		Запись.ДатаВозврата = '00010101';
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
